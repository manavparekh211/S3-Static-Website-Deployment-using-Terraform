resource "random_id" "bucket-suffix" {
  byte_length = 4
}

resource "aws_s3_bucket" "static-website" {
  bucket = "static-website-${random_id.bucket-suffix.hex}"
}

resource "aws_s3_bucket_public_access_block" "static-website" {
  bucket                  = aws_s3_bucket.static-website.id
  block_public_acls       = false
  block_public_policy     = false
  ignore_public_acls      = false
  restrict_public_buckets = false
}

resource "aws_s3_bucket_policy" "static-website-public-read-only" {
  bucket = aws_s3_bucket.static-website.id
  policy = jsonencode({
    "Version" : "2012-10-17",
    "Statement" : [
      {
        "Sid" : "PublicReadGetObject",
        "Effect" : "Allow",
        "Action" : "s3:GetObject",
        "Resource" : "${aws_s3_bucket.static-website.arn}/*",
        "Principal" : "*"
      }
    ]
  })
}

resource "aws_s3_bucket_website_configuration" "static-website-config" {
  bucket = aws_s3_bucket.static-website.id
  index_document {
    suffix = "index.html"
  }
  error_document {
    key = "error.html"
  }
}

resource "aws_s3_object" "index_html" {
  bucket       = aws_s3_bucket.static-website.id
  key          = "index.html"
  source       = "html-files/index.html"
  content_type = "text/html"
}

resource "aws_s3_object" "error_html" {
  bucket       = aws_s3_bucket.static-website.id
  key          = "error.html"
  source       = "html-files/error.html"
  content_type = "text/html"
}